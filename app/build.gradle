apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def getKeystoreFile = { ->
	def filePath = project.hasProperty('keystore') ? keystore.toString() : null
	def storeFile

	if (filePath != null)
	{
		storeFile = file(filePath)
	}

	if (storeFile == null && console != null)
	{
		storeFile = file(console.readLine("Enter keystore file path: ").trim())
	}

	println "Using keystore $storeFile"

	return storeFile
}

def getKeystorePassword = { ->
	def password = project.hasProperty('keystorePassword') ? keystorePassword.toString() : null

	if (password == null && console != null)
	{
		password = file(console.readLine("Enter keystore file path: ").trim())
	}

	println "Keystore password set to $password"

	return password
}

def getKeyAlias = { ->
	def alias = project.hasProperty('keyAlias') ? keyAlias.toString() : null

	if (alias == null && console != null)
	{
		alias = console.readLine("Enter key alias: ").trim()
	}

	println "Using signing key $alias"

	return alias
}

def getKeyPassword = { ->
	def password = project.hasProperty('keyPassword') ? keyPassword.toString() : null

	if (password == null && console != null)
	{
		password = console.readLine("Enter key password: ").trim()
	}

	return password
}

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.3"

	defaultConfig {
		applicationId "com.cube.arc.cie"
		minSdkVersion 17
		targetSdkVersion 25
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

		buildConfigField "String", "GA_CODE", "\"ABC-123\""
	}

	signingConfigs {
		release {
			storeFile getKeystoreFile()
			storePassword getKeystorePassword()
			keyAlias getKeyAlias()
			keyPassword getKeyPassword()
		}
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
		}
	}

	sourceSets {
		main {
			res.srcDirs += 'src/main/res-main'

			file('src/main/res-screen')
				.listFiles()
				.each { res.srcDirs += it.path }
		}
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
	implementation 'com.android.support:appcompat-v7:25.4.0'
	implementation 'com.android.support:design:25.4.0'
	implementation 'com.android.support:cardview-v7:25.4.0'
	implementation 'com.android.support.constraint:constraint-layout:1.0.2'

	implementation 'com.google.android.gms:play-services-analytics:11.4.0'
	implementation 'com.google.android.exoplayer:exoplayer:r2.4.3'
	implementation 'com.google.code.gson:gson:2.8.1'

	implementation 'com.atlassian.commonmark:commonmark:0.9.0'
	implementation 'com.squareup.okhttp3:okhttp:3.8.1'

	implementation 'org.kamranzafar:jtar:2.3'

	testImplementation 'junit:junit:4.12'
}
