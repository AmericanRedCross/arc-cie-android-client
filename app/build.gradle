apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def getGaCode = { -> return project.hasProperty('gaCode') ? gaCode.toString() : "" }
def getApiUrl = { -> return project.hasProperty('apiUrl') ? apiUrl.toString() : "" }
def getProjectId = { -> return project.hasProperty('projectId') ? projectId.toString() : "" }

android {
	compileSdkVersion 25
	buildToolsVersion '26.0.2'

	defaultConfig {
		applicationId "com.cube.arc.cie"
		minSdkVersion 17
		targetSdkVersion 25
		versionCode 3
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

		buildConfigField "String", "GA_CODE", "\"" + getGaCode() + "\""
		buildConfigField "String", "API_URL", "\"" + getApiUrl() + "\""
		buildConfigField "String", "PROJECT_ID", "\"" + getProjectId() + "\""
	}

	sourceSets {
		main {
			res.srcDirs += 'src/main/res-main'

			file('src/main/res-screen')
				.listFiles()
				.each { res.srcDirs += it.path }
		}
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})

	implementation project(':sdk:dmsdk')

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
	implementation 'com.android.support:appcompat-v7:25.4.0'
	implementation 'com.android.support:design:25.4.0'
	implementation 'com.android.support:cardview-v7:25.4.0'
	implementation 'com.android.support.constraint:constraint-layout:1.0.2'

	implementation 'com.google.android.gms:play-services-analytics:11.4.0'
	implementation 'com.google.android.exoplayer:exoplayer:r2.4.3'
	implementation 'com.google.code.gson:gson:2.8.1'

	implementation 'com.atlassian.commonmark:commonmark:0.9.0'
	implementation 'com.squareup.okhttp3:okhttp:3.8.1'

	implementation 'org.kamranzafar:jtar:2.3'

	testImplementation 'junit:junit:4.12'
}
